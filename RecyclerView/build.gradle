/*
 * Copyright 2019 Stephen Brewer
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 1
        versionName rootProject.version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
//        consumerProguardFiles 'consumer-proguard-rules.pro'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "androidx.core:core:1.1.0"
    implementation "androidx.customview:customview:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0-beta05"

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

publishing {
    publications {
        RecyclerView(MavenPublication) {
            artifact("$buildDir/outputs/aar/arch-ext-release.aar") {
                builtBy {assembleRelease}
            }
            artifact sourcesJar
            groupId 'org.stephenbrewer'
            artifactId 'recyclerview'
            version rootProject.version
        }
    }
}

bintray {
    user = "$bintray_user"
    key = "$bintray_key"
    publications = ['RecyclerView']
    pkg {
        repo = 'org.stephenbrewer'
        name = 'recyclerview'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/stephen-brewer/CommonCode.git'
    }
}
